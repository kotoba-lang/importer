(ns importer.connector-test
  (:require [clojure.edn :as edn]
            [clojure.test :refer [deftest is testing]]
            [connector.declare :as decl]
            [connector.model :as cm]
            [connector.provider :as cp]
            [connector.validate :as cv]
            [importer.connector :as c]))

(def d (c/descriptor))

(deftest the-corpus-descriptor-is-valid
  (is (empty? (cv/errors d)) (pr-str (cv/errors d))))

(deftest every-corpus-tool-is-a-read
  (is (= #{:read} (set (map :connector/effect (cm/tools d))))
      "nothing here writes, so nothing here is ever held for a human")
  (is (= (cm/tool-names d) (cm/tool-names (cm/read-only d)))
      "read-only removes nothing, because there is nothing to remove"))

(deftest a-bot-reading-the-corpus-holds-no-provider-scope
  (testing "no tool declares one"
    (is (empty? (mapcat :connector/scopes (cm/tools d))))
    (is (empty? (cm/scopes d))))
  (testing "and the kernel refuses to let one be added -- this is checked, not agreed"
    (let [smuggled (cm/add-tool d "corpus_sneaky"
                                {:description "reads the live mailbox"
                                 :effect :read
                                 :scopes ["https://www.googleapis.com/auth/gmail.readonly"]
                                 :input-schema {:type "object" :properties {}}})]
      (is (some #(= :tool/unexpected-scopes (:connector/code %)) (cv/errors smuggled)))
      (is (thrown? #?(:clj Exception :cljs js/Error)
                   (cp/provider smuggled {:request (fn [_ _] {})}))))))

(deftest requests-carry-no-credential
  (let [r (c/request "https://itonami.cloud" "corpus_mail_search" {"q" "quote" "limit" 20})]
    (is (= :get (:connector.http/method r)))
    (is (= "https://itonami.cloud/api/corpus/mail/search" (:connector.http/url r)))
    (is (= {"q" "quote" "limit" "20"} (:connector.http/query r)))
    (is (nil? (:connector.http/headers r))
        "connector.invoke attaches the Authorization header; nothing here can"))
  (is (thrown? #?(:clj Exception :cljs js/Error)
               (c/request "https://itonami.cloud" "corpus_delete_everything" {}))))

(deftest an-answer-without-coverage-is-refused
  (testing "a bare list is the shape this whole design exists to prevent"
    (is (= c/no-coverage
           (c/normalize "corpus_mail_search"
                        {:connector.http/status 200 :connector.http/body {:rows []}})))
    (is (= c/no-coverage
           (c/normalize "corpus_mail_search"
                        {:connector.http/status 200 :connector.http/body []}))))
  (testing "and an answer that states its coverage passes through"
    (let [body {:importer/rows [] :importer/coverage {} :importer/complete? false}]
      (is (= body (c/normalize "corpus_mail_search"
                               {:connector.http/status 200 :connector.http/body body})))))
  (testing "string keys too -- the JSON round trip must not defeat the check"
    (let [body {"importer/rows" [] "importer/coverage" {}}]
      (is (= body (c/normalize "corpus_mail_search"
                               {:connector.http/status 200 :connector.http/body body}))))))

(deftest the-provider-loads
  (let [p (c/provider)]
    (is (= "cloud.itonami.corpus" (cp/id p)))
    (is (contains? (set (cp/tool-names p)) "corpus_import_status"))))

(deftest connector-edn-matches-the-descriptor
  (testing "the committed declaration is generated, not maintained"
    (let [committed (edn/read-string
                     #?(:clj (slurp "connector.edn")
                        :cljs (.readFileSync (js/require "fs") "connector.edn" "utf8")))]
      (is (= (decl/declaration (c/provider)
                               {:namespace "importer.connector"
                                :var "provider"
                                :authority "90-docs/adr/2608301500-workspace-importer-plane.edn"})
             committed)
          "run: nbb --classpath \"src:../connector/src\" emit-connector-edn.cljs"))))
